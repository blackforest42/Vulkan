#version 450

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

// Texture index mappings
// 0 velocity
// 1 pressure
// 2 divergence
// 3 vorticity
// 4 density
// 5 temperature
layout (binding = 0) uniform sampler3D readOnlyTexs[];
layout (binding = 1) uniform writeonly image3D writeOnlyTexs[];

layout(binding = 2) uniform SimUBO {
    ivec3 gridSize;
} ubo;

vec3 getVelocity(ivec3 coord) {
    return texelFetch(readOnlyTexs[0], coord, 0).xyz;
}

void main() {
    ivec3 coord = ivec3(gl_GlobalInvocationID);

    // Compute curl of velocity field: w = grad x u
    vec3 vL = getVelocity(coord - ivec3(1, 0, 0));
    vec3 vR = getVelocity(coord + ivec3(1, 0, 0));
    vec3 vD = getVelocity(coord - ivec3(0, 1, 0));
    vec3 vU = getVelocity(coord + ivec3(0, 1, 0));
    vec3 vB = getVelocity(coord - ivec3(0, 0, 1));
    vec3 vF = getVelocity(coord + ivec3(0, 0, 1));

    vec3 curl;
    curl.x = (vU.z - vD.z - vF.y + vB.y);
    curl.y = (vF.x - vB.x - vR.z + vL.z);
    curl.z = (vR.y - vL.y - vU.x + vD.x);
    curl *= 0.5f;

    float magnitude = length(curl);
    imageStore(writeOnlyTexs[3], coord, vec4(curl, magnitude));
}
