#version 450
#extension GL_EXT_nonuniform_qualifier : enable

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout (binding = 0) uniform sampler3D readOnlyTexs[];
layout (binding = 1) uniform writeonly image3D writeOnlyTexs[];

layout(binding = 2) uniform SimParams {
    ivec3 gridSize;
} ubo;

// Texture index mappings
// 0 velocity
// 1 pressure
// 2 divergence
// 3 vorticity
// 4 density
// 5 temperature

bool isBoundary(ivec3 coord) {
    if (any(greaterThanEqual(coord, ubo.gridSize - 1)) || any(lessThanEqual(coord, ivec3(0)))) {
        return true;
    }
    return false;
}

float getPressure(ivec3 coord) {
    //if (isBoundary(coord)) return 0.0;
    return texelFetch(readOnlyTexs[1], coord, 0).r;
}

void main() {
    ivec3 coord = ivec3(gl_GlobalInvocationID);
    if (isBoundary(coord)) {
        return;
    }

    // Get neighboring pressures (treat solid neighbors as 0)
    float pL = getPressure(coord - ivec3(1, 0, 0));
    float pR = getPressure(coord + ivec3(1, 0, 0));
    float pD = getPressure(coord - ivec3(0, 1, 0));
    float pU = getPressure(coord + ivec3(0, 1, 0));
    float pB = getPressure(coord - ivec3(0, 0, 1));
    float pF = getPressure(coord + ivec3(0, 0, 1));

    float div = texelFetch(readOnlyTexs[2], coord, 0).r;

    // Count fluid neighbors
    int numFluid = 0;
    if (!isBoundary(coord - ivec3(1, 0, 0))) numFluid++;
    if (!isBoundary(coord + ivec3(1, 0, 0))) numFluid++;
    if (!isBoundary(coord - ivec3(0, 1, 0))) numFluid++;
    if (!isBoundary(coord + ivec3(0, 1, 0))) numFluid++;
    if (!isBoundary(coord - ivec3(0, 0, 1))) numFluid++;
    if (!isBoundary(coord + ivec3(0, 0, 1))) numFluid++;

    // Modified Jacobi
    float pressure = 0.0;
    if (numFluid > 0) {
        pressure = (pL + pR + pD + pU + pB + pF - div) / float(numFluid);
    }

    imageStore(writeOnlyTexs[1], coord, vec4(pressure));
}