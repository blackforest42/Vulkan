#version 450

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

// Texture index mappings
// 0 velocity
// 1 pressure
// 2 divergence
// 3 vorticity
// 4 density
// 5 temperature
layout (binding = 0) uniform sampler3D readOnlyTexs[];
layout (binding = 1) uniform writeonly image3D writeOnlyTexs[];

layout(binding = 2) uniform SimParams {
    ivec3 gridSize;
    vec3 gravity;
    float weightCoeff;
    float buoyancyCoeff;
    float ambientTemp;
    float timeStep;
} ubo;

bool isBoundary(ivec3 coord) {
    return (any(greaterThanEqual(coord, ubo.gridSize - 1)) || any(lessThanEqual(coord, ivec3(0))));
}

void main() {
    ivec3 coord = ivec3(gl_GlobalInvocationID);

    vec4 vel = texelFetch(readOnlyTexs[0], coord, 0);
    float temp = texelFetch(readOnlyTexs[5], coord, 0).r;
    float density = texelFetch(readOnlyTexs[4], coord, 0).r;

    float buoyancyMagnitude = -ubo.weightCoeff * density + ubo.buoyancyCoeff * (temp - ubo.ambientTemp);
    vec3 buoyancyForce = buoyancyMagnitude * ubo.gravity;

    vel.xyz += buoyancyForce * ubo.timeStep;

    imageStore(writeOnlyTexs[0], coord, vel);

}