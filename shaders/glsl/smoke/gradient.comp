#version 450

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout (binding = 0) uniform sampler3D readOnlyTexs[];
layout (binding = 1) uniform writeonly image3D writeOnlyTexs[];

layout(binding = 2) uniform SimParams {
    ivec3 gridSize;
    float cellSize;
} params;

// Texture index mappings
// 0 velocity
// 1 pressure
// 2 divergence
// 3 vorticity
// 4 density
// 5 temperature

bool isSolid(ivec3 coord) {
    if (any(lessThan(coord, ivec3(0))) || any(greaterThanEqual(coord, params.gridSize))) {
        return true;
    }
    return false;
}

float getPressure(ivec3 coord) {
    if (isSolid(coord)) return 0.0;
    return texelFetch(readOnlyTexs[1], coord, 0).r;
}

void main() {
    ivec3 coord = ivec3(gl_GlobalInvocationID);
    if (any(equal(coord, params.gridSize - 1)) || any(equal(coord, ivec3(0)))) return;
    
    // Don't modify velocity in solid cells
    if (isSolid(coord)) {
        imageStore(writeOnlyTexs[0], coord, vec4(0.0));
        return;
    }
    
    // Compute pressure gradient
    float pL = getPressure(coord - ivec3(1, 0, 0));
    float pR = getPressure(coord + ivec3(1, 0, 0));
    float pD = getPressure(coord - ivec3(0, 1, 0));
    float pU = getPressure(coord + ivec3(0, 1, 0));
    float pB = getPressure(coord - ivec3(0, 0, 1));
    float pF = getPressure(coord + ivec3(0, 0, 1));
    
    vec3 gradP = vec3(pR - pL, pU - pD, pF - pB) * 0.5 / params.cellSize;
    
    // Subtract gradient
    vec4 vel = texelFetch(readOnlyTexs[0], coord, 0);
    vel.xyz -= gradP;
    imageStore(writeOnlyTexs[0], coord, vel);
}