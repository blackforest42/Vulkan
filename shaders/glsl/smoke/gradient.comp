#version 450

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout (binding = 0) uniform sampler3D readOnlyTexs[];
layout (binding = 1) uniform writeonly image3D writeOnlyTexs[];

layout(binding = 2) uniform SimParams {
    ivec3 gridSize;
} ubo;

// Texture index mappings
// 0 velocity
// 1 pressure
// 2 divergence
// 3 vorticity
// 4 density
// 5 temperature

bool isBoundary(ivec3 coord) {
    if (any(greaterThanEqual(coord, ubo.gridSize - 1)) || any(lessThanEqual(coord, ivec3(0)))) {
        return true;
    }
    return false;
}

float getPressure(ivec3 coord) {
    return texelFetch(readOnlyTexs[1], coord, 0).r;
}

void main() {
    ivec3 coord = ivec3(gl_GlobalInvocationID);
    if (isBoundary(coord)) {
        imageStore(writeOnlyTexs[0], coord, vec4(0));
        return;
    }

    // Compute pressure gradient
    float pL = getPressure(coord - ivec3(1, 0, 0));
    float pR = getPressure(coord + ivec3(1, 0, 0));
    float pD = getPressure(coord - ivec3(0, 1, 0));
    float pU = getPressure(coord + ivec3(0, 1, 0));
    float pB = getPressure(coord - ivec3(0, 0, 1));
    float pF = getPressure(coord + ivec3(0, 0, 1));

    // Divide by two for derivative between 2 cells
    // e.g. (f(x + 1) - f(x)) / 2
    vec3 gradP = vec3(pR - pL, pU - pD, pF - pB) * 0.5f;

    // Subtract gradient
    vec4 vel = texelFetch(readOnlyTexs[0], coord, 0);
    vel.xyz -= gradP;
    imageStore(writeOnlyTexs[0], coord, vel);
}